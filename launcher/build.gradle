//buildscript {
//    repositories {
//        mavenCentral()
//    }
//
//    dependencies {
//        classpath 'org.springframework.boot:spring-boot-gradle-plugin:3.2.1'
//        classpath 'org.flywaydb:flyway-mysql:9.16.3'
//    }
//}

plugins {
    id "jacoco"
    alias(sharedLibs.plugins.spring.boot.plugin)
    alias(sharedLibs.plugins.flyway.plugin)
    alias(sharedLibs.plugins.spring.dependency.management.plugin)
    id "org.sonarqube" version "4.4.1.3373"
    id 'pl.allegro.tech.build.axion-release' version '1.16.1'
}

dependencies {
    implementation sharedLibs.spring.boot.starter.web
    implementation sharedLibs.spring.boot.starter.actuator
    implementation sharedLibs.spring.boot.starter.validation
    implementation sharedLibs.spring.boot.starter.logging
    implementation sharedLibs.spring.boot.starter.data.jpa

    runtimeOnly sharedLibs.flyway.mysql

    implementation sharedLibs.springdoc.openapi.starter.webmvc.ui

    implementation sharedLibs.spring.data.commons

    testImplementation sharedLibs.spring.boot.starter.test
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'io.rest-assured:json-schema-validator:5.4.0'
    testImplementation 'org.instancio:instancio-junit:3.6.0'
    testImplementation 'org.instancio:instancio-core:3.6.0'
    testImplementation project(":drivers:rest")
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'
    testImplementation 'org.testcontainers:localstack:1.19.3'

    implementation project(":adapter")
    implementation project(":drivers:rest")
    implementation project(":drivers:mysql")
}

scmVersion {
    repository {
        type = "git"
        remote = "origin"
    }

    versionIncrementer 'incrementPatch'
    branchVersionIncrementer = [
            'hotfix/.*': 'incrementPatch',
            'feature/.*': 'incrementMinor',
    ]
}

java {
    processResources {
        expand(project.properties)
    }
}

bootJar {
    archiveFileName = "product-service.jar"
}

springBoot {
    buildInfo {
        properties {
            name = 'Tech Challenge - Product API'
            additional = [
                    "description": project?.description ?= "",
                    "version"    : scmVersion.version
            ]
        }
    }
}

version = scmVersion.version

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.gradle.skipCompile', 'true'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'fiap-postech'
        property 'sonar.projectKey', 'fiap-postech_product-microservice'
    }
}

